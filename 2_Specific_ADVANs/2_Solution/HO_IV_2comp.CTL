$PROB HO_IV_2comp
$DATA ..//HOdata_IV.csv IGNORE=@
$INPUT ID TIME AMT DV MDV SEX AGE WT HT

$SUBROUTINE ADVAN3 TRANS4
$PK
;---- FIXED EFFECT DEFINITION ---- 
 TVCL = THETA(1)
 TVV1 = THETA(2)
 TVV2 = THETA(3)
 TVQ  = THETA(4)
 
;---- RANDOM EFFECT DEFINITION ----

 CL   = TVCL * EXP(ETA(1))
 V1   = TVV1 * EXP(ETA(2))
 V2   = TVV2 * EXP(ETA(3))
 Q    = TVQ  * EXP(ETA(4))
 
 S1 = V1
 
 KE   = CL/V1
 
$ERROR
 IPRED  = F
 W      = SQRT(THETA(5)**2 + THETA(6)**2 * IPRED**2)
 IRES   = DV - IPRED
 IWRES  = IRES / W
 Y      = IPRED + W * EPS(1)

$THETA
 (0, 10, 30)
 (0, 30, 100)
 (0, 50, 300)
 (0, 5, 20)
 
 0.001 FIX
 (0, 0.3, 1)

$OMEGA
 0.01
 0.01
 0.01
 0.01
 
$SIGMA
 1 FIX

$ESTIMATION NOABORT MAXEVAL=9999 METHOD=1 INTER PRINT=10 SIGDIGITS=3 

$TABLE ID TIME AMT DV MDV IPRED CWRES IWRES  ONEHEADER NOPRINT FILE = sdtab4
$TABLE ID ETA(1) ETA(2) ETA(3) ETA(4)        ONEHEADER NOPRINT NOAPPEND FILE = patab4
$TABLE ID SEX                                ONEHEADER NOPRINT NOAPPEND FILE = catab4
$TABLE ID AGE WT HT                          ONEHEADER NOPRINT NOAPPEND FILE = cotab4




