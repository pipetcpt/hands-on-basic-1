$PROB HO_PO_2comp
$DATA ..//HOdata_PO.csv IGNORE=@
$INPUT ID TIME AMT DV MDV SEX AGE WT HT

$SUBROUTINE ADVAN4 TRANS4
$PK
;---- FIXED EFFECT DEFINITION ---- 
 TVCL = THETA(1)
 TVV2 = THETA(2)
 TVV3 = THETA(3)
 TVQ  = THETA(4)
 TVKA = THETA(5)
 
;---- RANDOM EFFECT DEFINITION ----

 CL   = TVCL * EXP(ETA(1))
 V2   = TVV2 * EXP(ETA(2))
 V3   = TVV3 * EXP(ETA(3))
 Q    = TVQ  * EXP(ETA(4))
 KA   = TVKA * EXP(ETA(5))
 
 S2 = V2
 
 KE   = CL/V2
 
$ERROR
 IPRED  = F
 W      = SQRT(THETA(6)**2 + THETA(7)**2 * IPRED**2)
 IRES   = DV - IPRED
 IWRES  = IRES / W
 Y      = IPRED + W * EPS(1)

$THETA
 (0, 10, 30)
 (0, 30, 100)
 (0, 50, 300)
 (0, 5, 20)
 (0, 1.5, 5)
 
 0.001 FIX
 (0, 0.3, 1)

$OMEGA
 0.04
 0.04
 0 FIX
 0 FIX
 0 FIX
 
$SIGMA
 1 FIX

$ESTIMATION NOABORT MAXEVAL=9999 METHOD=1 INTER PRINT=10 SIGDIGITS=3 

$TABLE ID TIME AMT DV MDV IPRED CWRES IWRES  ONEHEADER NOPRINT FILE = sdtab1
$TABLE ID ETA(1) ETA(2)                      ONEHEADER NOPRINT NOAPPEND FILE = patab1
$TABLE ID SEX                                ONEHEADER NOPRINT NOAPPEND FILE = catab1
$TABLE ID AGE WT HT                          ONEHEADER NOPRINT NOAPPEND FILE = cotab1




